''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' "check_service.wsf"
' nagios@samurai.inka.de
'
' 02.06.2003 ver 1.1
' Minor bugfixes
' 
' --------------------------------------------------------------
' This plugin checks In what state a defined service Is.
'

<job>
<runtime>
  <description>
check_service (nrpe_nt-plugin) 1.1
The nagios plugins come with ABSOLUTELY NO WARRANTY. You may redistribute
copies of this plugin under the terms of the GNU General Public License.
For more information about these matters, see the file named COPYING.
Copyright (c) 1999-2001 Ethan Galstad/Hagen Deike (nagios@samurai.inka.de)

This plugin checks In what state a defined service Is.
  </description>
  <named
    name="h"
    helpstring="Help"
    type="simple"
    required="false" 
  />
  <named
    name="list"
    helpstring="Returns a list of all services by name"
    type="simple"
    required="false"
  />
  <unnamed
    name="service"
    helpstring="The name of the service to check"
    many="false"
    required="1"
  />
  <example>
Example: check_service.wsf /info
         Will return a large list of all services that are present.
         The output will look As follows:
         
         Service:     Alerter
         Description: Warndienst

         Service:     ALG
         Description: Gatewaydienst auf Anwendungsebene

         [...]
         
         To use the plugin you need the string that Is returned For service:
         In this Case If you wish To monitor the alerter you use this command
         
         check_service.wsf Alerter

         Please notice that the String Is Case sensitive
  </example>
</runtime>
<script language="VBScript">

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Const's and Var's
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Cons for return val's
Const intOK = 0
Const intWarning = 1
Const intCritical = 2
Const intUnknown = 3

' Cons for FSO
Const ForReading = 1
Const ForWriting = 2
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Help
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
If Wscript.Arguments.Named.Exists("info") Then
	strComputer = "."
	Set objWMIService = GetObject("winmgmts:" _
	& "{impersonationLevel=impersonate}!\\" & strComputer & "\root\cimv2")
	Set colRunningServices = objWMIService.ExecQuery("Select * from Win32_Service")

	For Each objService In colRunningServices
		Wscript.Echo "Service:     "& objService.Name
'		Wscript.Echo "State:       "& objService.State
		Wscript.Echo "Description: "& objService.DisplayName &Vbcrlf
	Next
	Wscript.Quit(intUnknown)
End If

If Wscript.Arguments.Named.Exists("h") Or Wscript.Arguments.Unnamed.Count <= 0 Then
	Wscript.Echo "Plugin help screen:"
      Wscript.Arguments.ShowUsage()
      Wscript.Quit(intUnknown)
End If
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Main
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

strComputer = "."
Set objWMIService = GetObject("winmgmts:" _
& "{impersonationLevel=impersonate}!\\" & strComputer & "\root\cimv2")
Set colRunningServices = objWMIService.ExecQuery("Select * from Win32_Service")
	
For intCounter = 0 To Wscript.Arguments.Unnamed.Count-1
	For Each objService In colRunningServices
		If Wscript.Arguments.Unnamed.Item(intCounter) = objService.Name Then

			If objService.State = "Stopped" Then
				Wscript.Echo "The service is Stopped"
				Wscript.Quit(intCritical)
			Elseif objService.State = "Start Pending" Then
				Wscript.Echo "The service is Start Pending"
				Wscript.Quit(intWarning)
			Elseif objService.State = "Stop Pending" Then
				Wscript.Echo "The service is Stop Pending"
				Wscript.Quit(intWarning)
			Elseif objService.State = "Running" Then
				Wscript.Echo "The service is Running"
				Wscript.Quit(intOK)
			Elseif objService.State = "Continue Pending" Then
				Wscript.Echo "The service is Continue Pending"
				Wscript.Quit(intWarning)
			Elseif objService.State = "Pause Pending" Then
				Wscript.Echo "The service is Pause Pending"
				Wscript.Quit(intWarning)
			Elseif objService.State = "Paused" Then
				Wscript.Echo "The service is Paused"
				Wscript.Quit(intCritical)
			Elseif objService.State = "Unknown" Then
				Wscript.Echo "The service state is Unknown"
				Wscript.Quit(intUnknown)
			End If
			
		End If 
	Next
Next

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
</script>
</job>
