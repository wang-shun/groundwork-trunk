#!/usr/local/groundwork/perl/bin/perl -w --
# MonArch - Groundwork Monitor Architect
# delete_service_instances
#
############################################################################
# Release 4.0
# December 2011
############################################################################
#
# Copyright 2011 GroundWork Open Source, Inc. (GroundWork)
# All rights reserved. This program is free software; you can redistribute
# it and/or modify it under the terms of the GNU General Public License
# version 2 as published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
#

# use diagnostics;
use strict;

use Getopt::Long qw(:config no_ignore_case auto_version);
use DBI;

# We use "our" instead of "my" so Getopt::Long auto_version processing can access this value.
our $VERSION = '1.0.0';

sub print_usage {
print
"usage:  delete_service_instances [-f] instance_id ...
   or:  delete_service_instances -h
where:  no options means check to see if all the specified
	    service instances exist, and if so, delete them all
	-f means force deletions:  delete all the specified service
	    instances that do exist, even if some of them do not exist
	-h means show this help message
	instance_id is a primary key value in the monarch.service_instance
	    table; for convenience, any commas appended to or interspersed
	    within the sequence of instance_id values will be ignored
";
}

my $all_is_done  = 0;
my $monarch_home = '';
my $sqlstmt      = '';
my $sth          = undef;

my $show_help = 0;
my $force     = 0;

eval {
    my $result = GetOptions (
	'h' => \$show_help,
	'f' => \$force
    );
    if (!$result) {
	print_usage();
	exit 1;
    }
};
if ($@) {
    chomp $@;
    print "FATAL:  Internal error:  $@\n";
    exit 1;
}

$show_help = 1 if not @ARGV;

if ($show_help) {
    print_usage();
    exit 1;
}

my ( $dbhost, $database, $user, $passwd ) = undef;
if ( -e "/usr/local/groundwork/config/db.properties" ) {
    open( FILE, '<', '/usr/local/groundwork/config/db.properties' )
      or die "\n\tCannot open the db.properties file ($!); aborting!\n";
    while ( my $line = <FILE> ) {
	if ( $line =~ /\s*monarch\.dbhost\s*=\s*(\S+)/ )   { $dbhost   = $1 }
	if ( $line =~ /\s*monarch\.database\s*=\s*(\S+)/ ) { $database = $1 }
	if ( $line =~ /\s*monarch\.username\s*=\s*(\S+)/ ) { $user     = $1 }
	if ( $line =~ /\s*monarch\.password\s*=\s*(\S+)/ ) { $passwd   = $1 }
    }
    close(FILE);
    $monarch_home = '/usr/local/groundwork/core/monarch';
}
else {
    print "\n\tMonarch Service Deletion";
    print "\n=============================================================\n";
    print "\n\tReading configuration file ...\n";

    until ($monarch_home) {
	if ( -e "/usr/local/groundwork/core/monarch/lib/MonarchConf.pm" ) {
	    $monarch_home = "/usr/local/groundwork/core/monarch";
	    print "\n\tPlease enter the Monarch installation path [ $monarch_home ] : ";
	    my $input = <STDIN>;
	    chomp $input;
	    if ($input) { $monarch_home = $input }
	    my $monarch_test = $monarch_home . '/lib/MonarchConf.pm';
	    unless ( -e $monarch_test ) {
		print "\n\tError: Cannot locate MonarchConf.pm in path $monarch_home [/lib] ...\n";
		$monarch_home = '';
	    }
	}
	else {
	    print "\n\tPlease enter the Monarch installation path : ";
	    my $input = <STDIN>;
	    chomp $input;
	    if ($input) { $monarch_home = $input }
	    my $monarch_test = $monarch_home . '/lib/MonarchConf.pm';
	    unless ( -e $monarch_test ) {
		print "\n\tError: Cannot locate MonarchConf.pm in path $monarch_home [/lib] ...\n";
		$monarch_home = '';
	    }
	}
    }
    open( FILE, '<', "$monarch_home/lib/MonarchConf.pm" );
    while ( my $line = <FILE> ) {
	$line =~ s/\'|\"|;//g;
	if ( $line =~ /\s*\$dbhost\s*=\s*(\S+)/ )   { $dbhost   = $1 }
	if ( $line =~ /\s*\$database\s*=\s*(\S+)/ ) { $database = $1 }
	if ( $line =~ /\s*\$dbuser\s*=\s*(\S+)/ )   { $user     = $1 }
	if ( $line =~ /\s*\$dbpass\s*=\s*(\S+)/ )   { $passwd   = $1 }
    }
    close(FILE);
}

##############################################################################
# Connect to DB
##############################################################################

print "\n\tConnecting to $database database with user $user ...\n";

my $dsn = "DBI:mysql:database=$database;host=$dbhost";
my $dbh = undef;

# We turn AutoCommit off because we want to make changes roll back automatically as much as
# possible if we don't get successfully through the entire script.  This is not perfect (i.e.,
# we don't necessarily have all the changes made in a single huge transaction) because some of
# the transformations may implicitly commit previous changes, and there is nothing we can do
# about that.  Still, we do the best we can.
#
# We turn PrintError off because RaiseError is on and we don't want duplicate messages printed.

eval { $dbh = DBI->connect( $dsn, $user, $passwd, { 'AutoCommit' => 0, 'RaiseError' => 1, 'PrintError' => 0 } ) };
if ($@) {
    print "\nError: connect failed ($@)\n";
    die;
}
$dbh->do( "set session transaction isolation level serializable" );

##############################################################################
# Execution
##############################################################################

#-----------------------------------------------------------------------------
# First, verify that all the specified services exist.
#-----------------------------------------------------------------------------

my $all_instance_ids_exist = 1;
my $got_duplicates = 0;
my @quoted_instance_ids = ();
my $ignored_instance_count = 0;

print "\n";

# For convenience in processing lists of service instances gathered from other tools, we
# preprocess the command-line arguments to allow comma-separated values without complaint.
foreach my $arg (@ARGV) {
    foreach my $instance_id (split /,/, $arg) {
	$instance_id =~ s/^\s+//;
	$instance_id =~ s/\s+$//;
	next if $instance_id eq '';
	if ($instance_id !~ /^\d+$/) {
	    print "\tFATAL:  '$instance_id' is not a valid instance_id value!\n";
	    exit 1;
	}
	my $quoted_instance_id = $dbh->quote($instance_id);
	$sqlstmt = "select count(*) from service_instance where instance_id=$quoted_instance_id";
	my @values = $dbh->selectrow_array($sqlstmt);
	if ($values[0]) {
	    push @quoted_instance_ids, $quoted_instance_id;
	}
	else {
	    $all_instance_ids_exist = 0;
	    ++$ignored_instance_count;
	    print "\tError:  instance_id $quoted_instance_id does not exist.\n";
	}
    }
}

#-----------------------------------------------------------------------------
# Then, delete whatever is appropriate.
#-----------------------------------------------------------------------------

if ($all_instance_ids_exist || $force) {
    if (@quoted_instance_ids) {
	$sqlstmt = 'delete from service_instance where instance_id=' . join(' or instance_id=', @quoted_instance_ids);
	$dbh->do($sqlstmt);
    }
    print "\n\tDeleted " . (scalar @quoted_instance_ids) . " service instance" . (@quoted_instance_ids == 1 ? '' : 's' ) .
	($ignored_instance_count ? ", ignored $ignored_instance_count service instance" . ($ignored_instance_count == 1 ? '' : 's') : '') . ".\n\n";
}
else {
    print "\n\tWARNING:  No deletions were done, because of errors above.\n\n";
}

##############################################################################
# Committing Changes 
##############################################################################

# Commit all previous changes.
my $rc = $dbh->commit();

##############################################################################
# Done.
##############################################################################

$all_is_done = 1;

END {
    if ($dbh) {
	# Roll back any uncommitted transaction.
	eval {
	    my $rc = $dbh->rollback();
	};
	if ($@) {
	    print "\n\tError:  rollback failed: ", $dbh->errstr, "\n";
	}
	$dbh->disconnect();
    }
    if (!$show_help && !$all_is_done) {
	print "\n";
	print "\t====================================================================\n";
	print "\t    WARNING:  service instance deletions did not fully complete!\n";
	print "\t====================================================================\n";
	print "\n";
	exit 1;
    }
}

# FIX MAJOR:  this is just wrong
exit ( ($all_instance_ids_exist || $force) && $all_is_done );

__END__

