#!/usr/local/groundwork/perl/bin/perl -w --

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use dassmonarch;

my $VERSION = "1.0.0";
( my $program = $0 ) =~ s{.*/}{};

use vars qw($opt_d $opt_V $opt_h $opt_H $opt_man);

# Parse Command Line Opts
Getopt::Long::Configure('bundling');
GetOptions(
    "V"            => \$opt_V,
    "version"      => \$opt_V,
    "h"            => \$opt_h,
    "help"         => \$opt_h,
    "m"            => \$opt_man,
    "man"          => \$opt_man,
    "d=s"          => \$opt_d,
    "debuglevel=s" => \$opt_d,
    "H=s"          => \$opt_H,
    "hostname=s"   => \$opt_H,
);

# GetOptions('-h' => \$opt_h, --man => \$opt_man) or pod2usage(2);
pod2usage( -verbose => 1 ) if $opt_h;
pod2usage( -exitval => 0, -verbose => 2 ) if $opt_man;

if ($opt_V) {
    print "$program version $VERSION.\n";
    exit 0;
}

if ( not $opt_H ) {
    print "$program:  hostname is not set; use -H to specify a hostname to delete\n";
    exit 1;
}

my $dm = dassmonarch->new();

$dm->set_debuglevel($opt_d) if $opt_d;

if ( not $dm->host_exists($opt_H) ) {
    $dm->debug( 'error', "$program:  host $opt_H was not found in the monarch database" );
    exit 1;
}

if ( $dm->delete_host($opt_H) ) {
    $dm->debug( 'info', "$program:  host $opt_H deleted" );
}
else {
    $dm->debug( 'error', "$program:  could not delete host $opt_H" );
}

__END__

=head1 NAME

monarch_delete_host

=head1 SYNOPSIS

monarch_delete_host -H hostname

=head1 DESCRIPTION

Deletes a host from the monarch database.

=head1 OPTIONS

-d [error,warning,info,verbose]

=head1 ORIGINAL AUTHOR

dass IT GmbH maik.aussendorf@dass-it.de

=cut

# vim: tabstop=8 shiftwidth=4 softtabstop=4 smarttab noexpandtab
