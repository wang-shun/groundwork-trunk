#!/usr/local/groundwork/perl/bin/perl -w --

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use dassmonarch;

my $VERSION = "1.0.0";
( my $program = $0 ) =~ s{.*/}{};

use vars qw($opt_d $opt_V $opt_h $opt_c $opt_s $opt_man);

# Parse Command Line Opts
Getopt::Long::Configure('bundling');
GetOptions(
    "V"             => \$opt_V,
    "version"       => \$opt_V,
    "h"             => \$opt_h,
    "help"          => \$opt_h,
    "m"             => \$opt_man,
    "man"           => \$opt_man,
    "d=s"           => \$opt_d,
    "debuglevel=s"  => \$opt_d,
    "c=s"           => \$opt_c,
    "commandline=s" => \$opt_c,
    "s=s"           => \$opt_s,
    "service=s"     => \$opt_s,
);

# GetOptions('-h' => \$opt_h, --man => \$opt_man) or pod2usage(2);
pod2usage( -verbose => 1 ) if $opt_h;
pod2usage( -exitval => 0, -verbose => 2 ) if $opt_man;

if ($opt_V) {
    print "$program version $VERSION.\n";
    exit 0;
}

if ( not $opt_c ) {
    print "$program:  commandline is not set; use -c to specify a commandline\n";
    exit 1;
}

if ( not $opt_s ) {
    print "$program:  service name is not set; use -s to specify a service\n";
    exit 1;
}

my $dm = dassmonarch->new();

$dm->set_debuglevel($opt_d) if $opt_d;

if ( not $dm->get_serviceid($opt_s) ) {
    $dm->debug( 'error', "$program:  service $opt_s was not found in the monarch database" );
    exit 1;
}

if ( $dm->set_generic_service_commandline( $opt_s, $opt_c ) ) {
    $dm->debug( 'info', "$program:  applied commandline \"$opt_c\" to service $opt_s" );
}
else {
    $dm->debug( 'error', "$program:  could not set commandline \"$opt_c\" for service $opt_s" );
    exit 1;
}

__END__

=head1 NAME

monarch_set_service_commandline

=head1 SYNOPSIS

monarch_set_service_commandline -s service -c commandline

=head1 DESCRIPTION

Sets the commandline for a given service.

=head1 OPTIONS

-d [error,warning,info,verbose]

=head1 ORIGINAL AUTHOR

dass IT GmbH maik.aussendorf@dass-it.de

=cut

# vim: tabstop=8 shiftwidth=4 softtabstop=4 smarttab noexpandtab
